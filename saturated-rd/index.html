<!DOCTYPE html>
<html>
    <head>
        <title>Saturated reaction-diffusion (Turing equations)</title>
        
        <link rel="stylesheet" type="text/css" href="../css/experiments.css"/>
        <link rel="stylesheet" type="text/css" href="../gradientui/gradientui.css"/>
        <link type="text/css" href="../3rd/custom-theme/jquery-ui-1.8.16.custom.css" rel="stylesheet" />
        <script type="text/javascript" src="../3rd/jquery-1.6.2.min.js"></script>
        <script type="text/javascript" src="../3rd/jquery-ui-1.8.16.custom.min.js"></script>
        <script type="text/javascript" src="../3rd/colorPicker.js"></script>
        <script type="text/javascript" src="../3rd/three.min.js"></script>
        <script type="text/javascript" src="../gradientui/gradientui.js"></script>
        
        <script type="x-shader/x-vertex" id="standardVertexShader">
            varying vec2 vUv;
            
            void main()
            {
                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
            }
        </script>
        
        <script type="x-shader/x-fragment" id="gsFragmentShader">
            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform sampler2D tSource;
            uniform float seed;
            uniform float diff_u;
            uniform float diff_v;
            uniform vec2 brush;
            uniform float aniso_u;
            uniform float aniso_v;
            uniform float limit_u;
            uniform float limit_v;
            uniform float split;
            uniform float play;

            float step_x = 1.0/screenWidth;
            float step_y = 1.0/screenHeight;
            vec2 texel = vec2(step_x, step_y);
            
            // https://thebookofshaders.com/10/
            // https://stackoverflow.com/questions/4200224/random-noise-functions-for-glsl
            float random (vec2 st)
            {
                return fract(sin(dot(st.xy, vec2(12.9898, 78.233))) * 43758.5453);
            }

            void main()
            {
                // explicit request to clear screen
                if (brush.x < -5.0) 
                {
                    // random noise (deterministic based on fragment position)
                    vec2 st = gl_FragCoord.xy;
                    st.x += seed;
                    float v = 4.0 + 4.0 * (random(st) - 0.5);
                    gl_FragColor = vec4(4.0, v, 0.0, 1.0);
                    return;
                }
                
                // simulation is paused: just copy texture pixels
                if (play == 0.0)
                {
                    vec2 uv = texture2D(tSource, vUv).rg;

                    if (brush.x > 0.0) {
                        vec2 diff = (vUv - brush) / texel;
                        float dist = dot(diff, diff);
                        if (dist < 8.0) {
                            //uv.r = 2.0;
                            uv.g = 0.0;
                        }
                    }

                    gl_FragColor = vec4(uv.r, uv.g, 0.0, 1.0);
                    return;
                }

                // creates a parameter map
                //float diff_u = vUv.y * 0.083;
                //float diff_v = vUv.x * 0.073;
                
                // five-point stencil
                vec2 uv  = texture2D(tSource, vUv).rg;
                vec2 uv0 = texture2D(tSource, vUv + vec2(-step_x, 0.0)).rg;
                vec2 uv1 = texture2D(tSource, vUv + vec2(step_x,  0.0)).rg;
                vec2 uv2 = texture2D(tSource, vUv + vec2(0.0,    -step_y)).rg;
                vec2 uv3 = texture2D(tSource, vUv + vec2(0.0,     step_y)).rg;
                
                vec2 lapl;
                if (aniso_u == 0.0) {
                    lapl.r =     uv0.r +     uv1.r + uv2.r + uv3.r - 4.0*uv.r;
                }
                else {
                    lapl.r = 0.5*uv0.r + 0.5*uv1.r + uv2.r + uv3.r - 3.0*uv.r;
                }
                if (aniso_v == 0.0) {
                    lapl.g =     uv0.g +     uv1.g + uv2.g + uv3.g - 4.0*uv.g;
                }
                else {
                    lapl.g = 0.5*uv0.g + 0.5*uv1.g + uv2.g + uv3.g - 3.0*uv.g;
                }

                // Gray-Scott equations
                //float du = 0.2097*lapl.r - uv.r*uv.g*uv.g + diff_u*(1.0 - uv.r);
                //float dv = 0.1050*lapl.g + uv.r*uv.g*uv.g - (diff_u+diff_v)*uv.g;

                // Turing equations
                float du = 0.01*(16.0 - uv.r*uv.g)        + diff_u*lapl.r;
                float dv = 0.01*(uv.r*uv.g - uv.g - 12.0) + diff_v*lapl.g;
                
                // fixed delta
                vec2 dst = uv + 0.3*vec2(du, dv);
                
                // clamp negative values of V
                if (dst.g < 0.0) {
                    dst.g = 0.0;
                }
                
                if ((split == 0.0) || (gl_FragCoord.x > screenWidth / 2.0)) {
                    // saturation limits for U and V
                    if (dst.r > limit_u) {
                        dst.r = limit_u;
                    }
                    if (dst.g > limit_v) {
                        dst.g = limit_v;
                    }
                }
                
                if (brush.x > 0.0) {
                    vec2 diff = (vUv - brush) / texel;
                    float dist = dot(diff, diff);
                    if (dist < 8.0) {
                        //dst.r = 2.0;
                        dst.g = 0.0;
                    }
                }
                
                gl_FragColor = vec4(dst.r, dst.g, 0.0, 1.0);
            }
        </script>
        
        <script type="x-shader/x-fragment" id="screenFragmentShader">
            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform sampler2D tSource;
            uniform vec4  color1;
            uniform vec4  color2;
            uniform vec4  color3;
            uniform vec4  color4;
            uniform vec4  color5;
            uniform float chem;
            
            vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
            
            void main()
            {
                float value;
                if (chem == 0.0) {
                    // show U in [2,7]
                    value = (texture2D(tSource, vUv).r - 2.0) / 5.0;
                }
                else {
                    // show V in [0,10]
                    value = texture2D(tSource, vUv).g / 10.0; 
                }
                
                float a;
                vec3 col;                
                if (value <= color1.a) {
                    col = color1.rgb;
                }
                if (value > color1.a && value <= color2.a) {
                    a = (value - color1.a)/(color2.a - color1.a);
                    col = mix(color1.rgb, color2.rgb, a);
                }
                if (value > color2.a && value <= color3.a) {
                    a = (value - color2.a)/(color3.a - color2.a);
                    col = mix(color2.rgb, color3.rgb, a);
                }
                if (value > color3.a && value <= color4.a) {
                    a = (value - color3.a)/(color4.a - color3.a);
                    col = mix(color3.rgb, color4.rgb, a);
                }
                if (value > color4.a && value <= color5.a) {
                    a = (value - color4.a)/(color5.a - color4.a);
                    col = mix(color4.rgb, color5.rgb, a);
                }
                if (value > color5.a) {
                    col = color5.rgb;
                }
                
            	gl_FragColor = vec4(col.r, col.g, col.b, 1.0);
            }
        </script>
        
        <script type="text/javascript" src="saturated-rd.js"></script>
        <meta charset="UTF-8">
        <style>
            #gradient
            {
                height: 40px;
                width: 85%;
                margin-top: 5px;
                margin-left:auto;
                margin-right:auto;
            }
        </style>
        <script>
            $(function()
            {
                $("#gradient").gradient({values:
                                        [[0.00, '#0000ff'],
                                         [0.25, '#00ffff'],
                                         [0.50, '#00ff00'],
                                         [0.75, '#ffff00'],
                                         [1.00, '#ff0000']]});
                
                // KLUDGE!
                colorPicker.offsetX = -512;
                colorPicker.offsetY = -256;
                
                document.getElementById("gradient").onselectstart = function () {return false;};
                
                init();
            });
        </script>
    </head>
    <body>
        <header>
            <h1>Saturated reaction-diffusion (Turing equations)</h1>
        </header>
        <div id="simulation">
        <canvas id="myCanvas" class="viewer" style="width:1024px;height:512px"></canvas>
        <aside class="panel">
            <section id="controls">
                Presets:
                <form name="ex">
                    <select name="scene" style="width:100%"
                        onchange="loadPreset(document.ex.scene.selectedIndex)">
                        <option value="0" selected="selected">Default</option>
                        <option value="1">Giraffe</option>
                        <option value="2">Nudibranch</option>
                        <option value="3">Moray #1</option>
                        <option value="4">Frog #1</option>
                        <option value="5">Frog #2 [stripes]</option>
                        <option value="6">Frog #2 [spots]</option>
                        <option value="7">Moray #2 [dark]</option>
                        <option value="8">Moray #2 [light]</option>
                    </select>
                </form>

                <p style="text-align:left">
                    <button id="btn_play" onclick="play()">Play / Pause</button>
                    Iteration:
                    <span class="remark" id="iteration"></span>
                <p/>

                <p style="text-align:left">
                    <button id="btn_clear" onclick="clean()">Clear</button>
                    <button id="btn_select_u" onclick="select_u()">Show U</button>
                    <button id="btn_select_v" onclick="select_v()">Show V</button>                    
                    <button id="btn_snapshot" onclick="snapshot()">Take snapshot</button>
                <p/>

                U diffusion rate:
                <span class="remark" id="diff_u"></span><br/>
                <div id="sld_diff_u" class="slider"></div>
                
                V diffusion rate:
                <span class ="remark" id="diff_v"></span><br/>
                <div id="sld_diff_v" class="slider"></div>

                U saturation limit:
                <span class="remark" id="limit_u"></span><br/>
                <div id="sld_limit_u" class="slider"></div>
                
                V saturation limit:
                <span class="remark" id="limit_v"></span><br/>
                <div id="sld_limit_v" class="slider"></div>
                
                Split domain / anisotropy:
                <p style="text-align:center">
                    <button id="btn_split" onclick="toggle_split()">Toggle split domain</button>
                    <button id="btn_aniso_u" onclick="aniso_u()">Toggle anisotropy for U</button>
                    <button id="btn_aniso_v" onclick="aniso_v()">Toggle anisotropy for V</button>
                <p/>
                
                Colors:
                <div id="gradient" style=""></div>
                <div style="font-size:x-small">Share (paste and press enter):</div>
                <div style="text-align:center"><input id="share" style="width:85%"/></div>
            </section>
        </aside>
        </div>
        <footer>
                <div id="copyright">
                        By Marcelo Malheiros, based on
                        <a href="https://github.com/pmneila/jsexp">code</a> from
                        <a href="https://twitter.com/pmneila">@pmneila</a>
                </div>
        </footer>
    </body>
</html>

